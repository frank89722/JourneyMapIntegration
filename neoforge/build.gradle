plugins {
    id 'io.github.CDAGaming.cursegradle' version '1.+'
    id "com.modrinth.minotaur" version "2.+"
    id 'net.neoforged.gradle.userdev' version '7.0.170'
}

archivesBaseName = "${mod_id}-neoforge"

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'mixin.debug.export', 'true'
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }
}


sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"

    compileOnly project(':api')
    compileOnly project(":common")

    implementation "dev.architectury:architectury-neoforge:${project.architectury_version}"

    compileOnly project(':compat-ftbchunks')
    implementation "dev.ftb.mods:ftb-library-neoforge:${project.ftb_library_version}"
    implementation "dev.ftb.mods:ftb-teams-neoforge:${project.ftb_teams_version}"
    implementation "dev.ftb.mods:ftb-chunks-neoforge:${project.ftb_chunks_version}"

    compileOnly project(':compat-waystones')
    implementation "maven.modrinth:balm:${project.balm_neoforge_version}"
    implementation "maven.modrinth:waystones:${project.waystones_neoforge_version}"

    api group: 'info.journeymap', name: 'journeymap-api-neoforge', version: project.journeymap_api_version, changing:
            true
    implementation "maven.modrinth:journeymap:${project.jm_neoforge_version}"

}

//Spec<Task> notNeoTask = { Task it -> !it.name.startsWith("neo") } as Spec<Task>

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
    source(project(":api").sourceSets.main.allSource)
    source(project(":compat-waystones").sourceSets.main.allSource)
    source(project(":compat-ftbchunks").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
    from project(":api").sourceSets.main.resources
    from project(":compat-waystones").sourceSets.main.resources
    from project(":compat-ftbchunks").sourceSets.main.resources
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Build-On-Minecraft'      : minecraft_version,
        ])
    }

}

def ENV = System.getenv()
def release_title = "[NeoForge] v${version}"

if (ENV.CF_TOKEN) {
    curseforge {
        apiKey = ENV.CF_TOKEN
        project {
            id = curse_id
            releaseType = release_type
            addGameVersion "NeoForge"
            addGameVersion "Java 21"
            addGameVersion "Client"
            addGameVersion minecraft_version
            mainArtifact(jar) {
                it.displayName = release_title
            }
            relations {
                it.requiredDependency 'journeymap'
                it.optionalDependency 'ftb-chunks-forge'
                it.optionalDependency 'waystones'
            }
            changelog = rootProject.file('changelog.md')
            changelogType = 'markdown'
        }
    }

}

if (ENV.MODRINTH_TOKEN) {
    modrinth {
        token = ENV.MODRINTH_TOKEN
        projectId = modrinth_id
        versionType = release_type
        versionName = release_title
        versionNumber = "${version}+neoforge"
        uploadFile = jar
        gameVersions = [minecraft_version]
        changelog = rootProject.file('changelog.md').text
        dependencies {
            required.project "lfHFW1mp" // journeymap
            optional.project "LOpKHB2A" // waystones
        }
    }
}
