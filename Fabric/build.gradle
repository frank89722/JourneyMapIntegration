plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

def ENV = System.getenv()

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

repositories {
    maven { url "https://maven.shedaniel.me/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":Common")

    modApi group: 'info.journeymap', name: 'journeymap-api', version: project.journeymap_api_fabric_version, changing: true
    // this should be updated to runtimeOnly -> though you have some hard dependencies on the base mod.
//    modRuntimeOnly "curse.maven:journeymap-${project.jm_project_id}:${project.jm_fabric_file_id}"
    modImplementation "curse.maven:journeymap-${project.jm_project_id}:${project.jm_fabric_file_id}"

    modImplementation "dev.architectury:architectury-fabric:4.2.50"
    modImplementation "dev.ftb.mods:ftb-library-fabric:1802.3.6-build.115"
    modImplementation "dev.ftb.mods:ftb-teams-fabric:1802.2.6-build.51"
    modImplementation "dev.ftb.mods:ftb-chunks-fabric:1802.3.5-build.130"
    modImplementation "curse.maven:balm-500525:3684724"
    modImplementation "curse.maven:waystones-500087:3684734"
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}


processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

if (ENV.CF_TOKEN) {
    curseforge {
        apiKey = ENV.CF_TOKEN
        project {
            id = "${curse_id}"
            releaseType = "${release_type}"
            addGameVersion "Fabric"
            addGameVersion "${minecraft_version}"
            mainArtifact(jar)
            relations {
                requiredDependency 'journeymap'
                optionalDependency 'ftb-chunks-fabric'
                optionalDependency 'waystones-fabric'
            }
            changelog = ENV.CHANGELOG
            changelogType = 'markdown'
        }
    }

    afterEvaluate {
        tasks.curseforge525447.dependsOn remapJar
    }
}
