plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

def ENV = System.getenv()

version = "${version}-${ENV.GITHUB_RUN_NUMBER ?: 'dev'}"
archivesBaseName = "${mod_id}-fabric-${minecraft_version}"

repositories {
    maven { url "https://maven.shedaniel.me/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":Common")

    modApi group: 'info.journeymap', name: 'journeymap-api', version: project.journeymap_api_fabric_version, changing: true
    modRuntimeOnly "curse.maven:journeymap-${project.jm_project_id}:${project.jm_fabric_file_id}"

    modImplementation "dev.architectury:architectury-fabric:${project.architectury_version}"
    modImplementation "dev.ftb.mods:ftb-library-fabric:${project.ftb_library_version}"
    modImplementation "dev.ftb.mods:ftb-teams-fabric:${project.ftb_teams_version}"
    modImplementation "dev.ftb.mods:ftb-chunks-fabric:${project.ftb_chunks_version}"
    modImplementation "curse.maven:balm-${project.balm_fabric_project_id}:${project.balm_fabric_file_id}"
    modImplementation "curse.maven:waystones-${project.waystones_fabric_project_id}:${project.waystones_fabric_file_id}"
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}


processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

if (ENV.CF_TOKEN) {
    curseforge {
        apiKey = ENV.CF_TOKEN
        project {
            id = "${curse_id}"
            releaseType = "${release_type}"
            addGameVersion "Fabric"
            addGameVersion "Quilt"
            addGameVersion "${minecraft_version}"
            mainArtifact(remapJar.archivePath)
            relations {
                requiredDependency 'journeymap'
                requiredLibrary 'fabric-api'
                optionalDependency 'ftb-chunks-fabric'
                optionalDependency 'waystones-fabric'
            }
            changelog = rootProject.file('changelog.md')
            changelogType = 'markdown'

        }

        options {
            forgeGradleIntegration = false
        }
    }
}

if (ENV.MODRINTH_TOKEN) {
    modrinth {
        token = ENV.MODRINTH_TOKEN
        projectId = "${modrinth_id}"
        versionType = "${release_type}"
        versionNumber = "${archivesBaseName}-${version}"
        uploadFile = remapJar
        gameVersions = ["${minecraft_version}"]
        loaders = ["fabric", "quilt"]
        changelog = rootProject.file('changelog.md')
        dependencies {
            required.project "P7dR8mSH" // fabric-api
            required.project "lfHFW1mp" // journeymap
        }
    }
}
