buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

def ENV = System.getenv()

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

version = "${MC_VERSION}-${VERSION}-${ENV.GITHUB_RUN_NUMBER ?: 'dev'}"
group = 'frankv.jmi' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'jmi'

java.toolchain.languageVersion = JavaLanguageVersion.of(8) // Mojang ships Java 8 to end users, so your mod should target Java 8.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

println("##[set-output name=modid;]${archivesBaseName}")
println("##[set-output name=version;]${version}")

minecraft {
    mappings channel: 'official', version: '1.16.5'
    runs {
        client {
            workingDirectory project.file('run')
            properties 'mixin.env.remapRefMap': 'true'
            //property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            arg "-mixin.config=jmi.mixins.json"

            mods {
                jmi {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            properties 'mixin.env.remapRefMap': 'true'
            //property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            arg "-mixin.config=jmi.mixins.json"

            mods {
                jmi {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'jmi', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                jmi {
                    source sourceSets.main
                }
            }
        }
    }
}


// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://maven.architectury.dev"
        content {
            includeGroup "me.shedaniel"
        }
    }

    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    maven {
        url "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.ftb.mods"
        }
    }

    maven {
        url "https://jm.gserv.me/repository/maven-snapshots/"
        content {
            includeGroup "info.journeymap"
        }
    }
    jcenter()
    mavenCentral()
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.20'

    compile 'org.spongepowered:mixin:0.8'
    annotationProcessor "org.spongepowered:mixin:0.8.2:processor"

    implementation fg.deobf("me.shedaniel:architectury-forge:1.20.28")
    implementation fg.deobf("dev.ftb.mods:ftb-library-forge:1605.3.4-build.90")
    implementation fg.deobf("dev.ftb.mods:ftb-teams-forge:1605.2.3-build.40")
    implementation fg.deobf("dev.ftb.mods:ftb-chunks-forge:1605.3.2-build.95")
    implementation fg.deobf("curse.maven:waystones-245755:3440017")
    implementation fg.deobf("curse.maven:journeymap-32274:3634141")
    implementation fg.deobf("curse.maven:smoothboot-420913:3224252")
    //compileOnly fg.deobf("info.journeymap:journeymap-api:1.16.5-1.7-SNAPSHOT")

}

mixin {
    add sourceSets.main, "jmi.refmap.json"
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
            "Specification-Title": "jmi",
            "Specification-Vendor": "frankV",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"frankV",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "jmi.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

if (ENV.CF_TOKEN) {
    curseforge {
        apiKey = ENV.CF_TOKEN
        project {
            id = "${CURSE_ID}"
            releaseType = "${RELEASE_TYPE}"
            addGameVersion "Forge"
            addGameVersion "${MC_VERSION}"
            mainArtifact(jar)
            relations {
                requiredDependency 'journeymap'
                optionalDependency 'ftb-chunks-forge'
                optionalDependency 'waystones'
            }
            changelog = ENV.CHANGELOG
            changelogType = 'markdown'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
