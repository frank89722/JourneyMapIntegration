plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'maven-publish'
}

def ENV = System.getenv()

version = "${version}-${ENV.GITHUB_RUN_NUMBER ?: 'dev'}"
archivesBaseName = "${mod_id}-common-${minecraft_version}"

loom {
    remapArchives = false
    setupRemappedVariants = false
    enableTransitiveAccessWideners = false
    interfaceInjection {
        enableDependencyInterfaceInjection = false
    }
    runConfigs.configureEach {
        ideConfigGenerated = false
    }
}

dependencies {
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modCompileOnly("net.fabricmc:fabric-loader:${fabric_loader_version}")

    modCompileOnlyApi group: 'info.journeymap', name: 'journeymap-api', version: project.journeymap_api_common_version, changing: true

    modCompileOnly "dev.architectury:architectury-fabric:${project.architectury_version}"
    modCompileOnly "dev.ftb.mods:ftb-library-fabric:${project.ftb_library_version}"
    modCompileOnly "dev.ftb.mods:ftb-teams-fabric:${project.ftb_teams_version}"
    modCompileOnly "dev.ftb.mods:ftb-chunks-fabric:${project.ftb_chunks_version}"
    modCompileOnly "curse.maven:balm-${project.balm_fabric_project_id}:${project.balm_fabric_file_id}"
    modCompileOnly "curse.maven:waystones-${project.waystones_fabric_project_id}:${project.waystones_fabric_file_id}"

}

processResources {

    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {

        expand buildProps
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

remapJar.enabled = false
remapSourcesJar.enabled = false